#!/usr/bin/zsh
# zsh web server

# Options
# Clear env
for z in ${(SM)$(export)#*=}; do
    case ${z%=} in
        (PWD|_|HOME)
            continue;;
        (*)
            unset ${z%=};;
    esac
done

export PATH=/bin:/usr/bin

# Load Modules
zmodload zsh/net/tcp
zmodload zsh/files
zmodload zsh/stat
zmodload zsh/datetime
zmodload zsh/system
zmodload zsh/zselect

# zsh options
unsetopt multibyte
setopt posix_jobs

# Declare variable types
integer logfd
integer fifofd

typeset -a CGI_EXTS_ARRAY
typeset -a GZIP_TYPES_ARRAY

# Internal function variables that need global scope
typeset -A header
typeset -a dotdir dotfiles dir files
typeset -a cgi_head cgi_body
typeset cgi_status_code

# Functions
function error() {
    print "Error: $*" >&2 >&$logfd

    exec {logfd}>&-
    rm $FIFO 2>/dev/null
    ztcp -c; exit 113
}

function logger() {
    print "[$(strftime "%a %d %b, %H:%M:%S" $EPOCHSECONDS)] $*" >&$logfd
}

function logger_child() {
    print "[$(strftime "%a %d %b, %H:%M:%S" $EPOCHSECONDS)] [pid: $sysparams[pid]] (client $client_ip): $header[method] $header[url] $header[version] $*" >&$logfd
}

function timeout() {
    local pid1 pid2

    function TRAPCHLD() {
        kill $pid1 $pid2 2>/dev/null
    }

    $* &; pid1=$!
    sleep $CGI_TIMEOUT &; pid2=$!

    wait $pid1
    kill $pid2 2>/dev/null
}

function ip_addr() {
    local -a tcp_list
    local -a ip

    tcp_list=("${(f@)$(</proc/net/tcp)}")

    for i v in "${(s..)tcp_list[4][7,14]}"; do
        ip+="0x${(L)i}${(L)v}"
    done

    printf "%d.%d.%d.%d\n" ${(Oa)ip[@]}
}

function help() {
<<EOF
Usage: czhttpd [OPTIONS] <file or dir>

czhttpd - cloud's zsh http server

Configuration Options
    -C :    Config file location (default: ~/.config/czhttpd/czhttpd.conf)

Connection Options
    -c :    Force connection: close for all HTTP/1.1 response headers
    -m :    Max number of connections to accept (default: 12)
    -p :    Port to bind to (default: 8080)
    -t :    Connection timeout in seconds (default: 30)

File Options
    -a :    Display hidden files in directories
    -i :    Specify index file (default: index.html)
    -s :    Allow czhttpd to follow symlinks
    -x :    Comma delimited list of file extensions to treat as
            CGI scripts
    -z :    Enable gzip compression for text/{html,js,css}. An
            optional comma delimited list of file mimetypes can
            be specified for additional file types to compress.

Output Options
    -l :    Enable logging to existing file
    -v :    Print log messages to stdout
    -h :    Print this help message

If no file or directory is given, czhttpd defaults to serving
the current directory.
EOF

exit
}

function parse_conf() {
    if [[ -f $1 ]]; then
        . $1 || error "Cannot source config file $1"

        (( $DISPLAY_HIDDEN_FILES )) && setopt GLOB_DOTS

        if [[ -n $CGI_EXTS ]]; then
            for k in ${(s.,.)CGI_EXTS}; CGI_EXTS_ARRAY+="${k##*.}"
        fi
        if [[ $COMPRESSION == 1 && -n $COMPRESSED_TYPES ]]; then
            for k in ${(s.,.)COMPRESSED_TYPES}; GZIP_TYPES_ARRAY+="$k"
        fi

        if [[ ! -f $LOG_FILE ]]; then
            error "Log file $LOG_FILE does not exist."
        elif [[ $LOG_FILE != "/dev/null" ]]; then
            exec {logfd}>>$LOG_FILE
        fi
    fi
}

function parse_args() {
    zparseopts -K -D -A OPTS C: i: l: m: p: t: x: z:: a c h s v || error 2>/dev/null

    [[ -n $OPTS[-C] && ! -f $OPTS[-C] ]] && error "Config file $OPTS[-C] does not exist"
    parse_conf ${OPTS[-C]:-~/.config/czhttpd/czhttpd.conf}

    [[ -n ${(kM)OPTS:#-h} ]] && help
    [[ -n ${(k)OPTS[-l]} && -n ${(k)OPTS[-v]} ]] && error "Can not output to both stdout and logfile"

    for i in ${(k)OPTS}; do
        case $i in
            (-a)
                setopt GLOB_DOTS;;
            (-c)
                HTTP_KEEP_ALIVE=0;;
            (-i)
                INDEX_FILE=$OPTS[$i];;
            (-l)
                [[ ! -f $OPTS[$i] ]] && error "File $OPTS[$i] does not exist"
                exec {logfd}>>$OPTS[$i];;
            (-m)
                [[ $OPTS[$i] != <-> ]] && error "$OPTS[$i] is not a valid integer"
                MAX_CONNECTIONS=$OPTS[$1];;
            (-p)
                if [[ $OPTS[$i] != <-> ]]; then
                    error "$OPTS[$i] is not a valid integer"
                else
                    (( $OPTS[$i] < 1024 )) && error "$OPTS[$i] is not a valid port number"
                fi
                PORT=$OPTS[$i];;
            (-s)
                FOLLOW_SYMLINKS=1;;
            (-t)
                [[ $OPTS[$i] != <-> ]] && error "$OPTS[$i] is not a valid integer"
                HTTP_TIMEOUT=$OPTS[$i];;
            (-v)
                exec {logfd}>&1 2>/dev/null;;
            (-x)
                unset CGI_EXTS_ARRAY && typeset -ga CGI_EXTS_ARRAY
                for k in ${(s.,.)OPTS[$i]}; CGI_EXTS_ARRAY+="${k##*.}";;
            (-z)
                unset GZIP_TYPES_ARRAY && typeset -ga GZIP_TYPES_ARRAY
                for k in ${(s.,.)OPTS[$i]}; GZIP_TYPES_ARRAY+="$k"
                COMPRESSION=1;;
        esac
    done

    logger "> Initializing czhttpd"

    if [[ -n $1 ]]; then
        [[ -e $1 ]] || error "$1 is not a valid directory or file."
        DOCROOT="$1"
    else
        DOCROOT="$PWD"
    fi
}

function http_listen() {
    ztcp -l $PORT 2>/dev/null || error "Could not bind to port $PORT"
    print "Listening on $PORT"
    listenfd=$REPLY
}

function parse_request() {
    unset header; typeset -gA header

    read -t $HTTP_TIMEOUT -r -u $fd line || return 1
    for method url version in ${(s. .)line%$'\r'}; do
        header[method]="$method"
        header[url]="${url%\?*}"
        header[querystr]="${(M)url%\?*}"
        header[version]="$version"
    done

    while read -t $HTTP_TIMEOUT -r -u $fd line; do
        [[ -n $line && $line != $'\r' ]] || break
        for key value in ${(s/: /)line%$'\r'}; do
            header[${(L)key}]="$value"
        done
    done
}

function check_request() {
    case $header[version] in
        ("HTTP/1.1")
            [[ -z $header[host] ]] && { error_header 400; return 1 };;
        ("HTTP/1.0")
            continue;;
        (*)
            error_header 505
            return 1;;
    esac

    case $header[method] in
        ("HEAD"|"GET"|"POST")
            return 0;;
        (*)
            error_header 501
            return 1;;
    esac
}

function return_header() {
    print -n "HTTP/1.1 $1\r\n"

    if [[ $HTTP_KEEP_ALIVE == 0 || $header[version] != "HTTP/1.1" ]]; then
        print -n "Connection: close\r\n"
    else
        print -n "Connection: ${header[connection]:-keep-alive}\r\n"
    fi

    print -n "Date: $(export TZ=UTC && strftime "%a, %d %b %Y %H:%M:%S" $EPOCHSECONDS) GMT\r\n"
    print -n "Server: czhttpd\r\n"
    for i in "$@[2,-1]"; do
        print -n "$i\r\n"
    done
    print -n "\r\n"
}

function error_header() {
    local message
    local -a add_headers

    case "$1" in
        (301)
            message="301 Moved Permanently"
            add_headers+="Location: $2";;
        (400)
            message="400 Bad Request";;
        (403)
            message="403 Forbidden";;
        (404)
            message="404 Not Found";;
        (500)
            message="500 Internal Server Error";;
        (501)
            message="501 Not Implemented";;
        (505)
            message="505 HTTP Version Not Supported";;
    esac

    logger_child "$message"
    return_header "$message" "Content-type: text/plain; charset=UTF-8" "Content-Length: ${#message}" $add_headers[@]
    print "$message"
}

function html_template() {
<<EOF
<!DOCTYPE html>
<html>
<head>
<style type="text/css">
a {text-decoration: none;}
a:hover, a:focus { color: white; background: rgba(0,0,0,0.3); cursor: pointer; }
h2 { margin-bottom: 10px }
table { border-collapse: collapse; }
thead th { padding-top: 4px; padding-bottom: 6px; text-align: left; }
thead th:nth-child(2) { text-align: right; padding-right: 12px; }
tbody td:nth-child(2) { text-align: right; padding-right: 12px; }
tbody td:first-child { padding-right: 30px; }
div.list { background-color: #F5F5F5; border-top: 1px solid black; border-bottom: 1px solid black; font: 90% monospace; margin: 4px;}
div.footer { font: 90% monospace; color: grey; font-style: italic; padding-left: 4px; }
</style>
<title>czhttpd</title>
</head>
<body>
<h2>Index of $1</h2>
<div class=list>
    <table>
        <thead>
        <tr>
            <th>Name</th>
            <th>Size</th>
            <th>Type</th>
        </tr>
        </thead>
        <tbody>
        $@[2,-1]
        </tbody>
    </table>
</div>
<div class=footer>$SERVER_SOFTWARE</div>
</body>
</html>
EOF
}

function html_fragment() {
<<EOF
<tr><td><a href="${1#*/}">${1#*/}</a></td>
<td>$(calc_size $1)</td>
<td>$(mime_type $1)</td></tr>
EOF
}

function urlencode() {
    for i in ${(s::)1}; do
        case "$i" in
            ([-._~A-Za-z0-9])
                printf '%s' "$i";;
            (*)
                printf '%%%02x' "'$i";;
        esac
    done

    printf '\n'
}

function urldecode() {
    printf '%b\n' "${1:gs/%/\\x}"
}

function mime_type() {
    [[ -d "$1" ]] && { print "Directory"; return }

    case $1 in
        (*.html)
            print "text/html";;
        (*.css)
            print "text/css";;
        (*)
            mtype=$(file --mime-type -bL $1)

            case $mtype in
                (*"no read permission")
                    print "application/octet-stream";;
                ("text/"*)
                    print "text/plain";;
                (*)
                    print "${mtype#application/x-executable}";;
            esac;;
    esac
}

function calc_size() {
    [[ -d "$1" ]] && { print "\-"; return }

    KB=1024.0
    MB=1048576.0
    GB=1073741824.0
    
    size=$(stat -L +size $1)

    (( $size < $KB )) && { printf '%.1f%s\n' "${size}" "B" && return }
    (( $size < $MB )) && { printf '%.1f%s\n' "$((size/$KB))" "K" && return }
    (( $size < $GB )) && { printf '%.1f%s\n' "$((size/$MB))" "M" && return }
    (( $size > $GB )) && { printf '%.1f%s\n' "$((size/$GB))" "G" && return }
}

function list_dir() {
    unset dir dotdir files dotfiles
    typeset -ga dir dotdir files dotfiles

    cd "$1" || return 1

    [[ "${1%/}" != "${DOCROOT%/}" ]] && dotdir+="$(html_fragment '/../')"

    for i in ./*(Nr); do
        if [[ $i == ./.* ]]; then
            [[ -d "$i" ]] && dotdir+="$(html_fragment $i/)"
            [[ -f "$i" ]] && dotfiles+="$(html_fragment $i)"
            continue
        elif [[ -d "$i" ]]; then
            dir+="$(html_fragment $i/)"
        else
            files+="$(html_fragment $i)"
        fi
    done

    cd -
}

function check_if_compression() {
    [[ -z $GZIP_TYPES_ARRAY ]] && return 1
    [[ -z ${(SM)header[accept-encoding]#gzip} ]] && return 1

    if [[ -f $1 ]]; then
        for i in $GZIP_TYPES_ARRAY; [[ $mtype == $i ]] && break

        if [[ $? != 0 ]] || (( $file_array[size] < $GZIP_MIN_SIZE )); then
            return 1
        fi
    fi

    return 0
}

function send_list() {
    local -a resp_headers

    list_dir $1 || return 1

    resp_headers=("200 Ok" "Content-type: text/html; charset=UTF-8" "Transfer-Encoding: chunked")

    if check_if_compression; then
        resp_headers+=("Content-Encoding: gzip")

        return_header $resp_headers[@]
        html_template $1 $dotdir $dotfiles $dir $files | gzip -$GZIP_LEVEL -c | send_chunk
    else
        return_header $resp_headers[@]
        if [[ $header[method] != "HEAD" ]]; then
            html_template $1 $dotdir $dotfiles $dir $files | send_chunk
        fi
    fi
}

function send_compressed_file() {
    return_header "200 Ok" "Content-type: $mtype; charset=UTF-8" "Content-Encoding: gzip" "Transfer-Encoding: chunked"

    gzip -$GZIP_LEVEL -c $1 | send_chunk
}

function send_raw_file() {
    return_header "200 Ok" "Content-type: $mtype; charset=UTF-8" "Content-Length: $file_array[size]"

    if [[ $header[method] != "HEAD" ]]; then
        <$1
    fi
}

function send_chunk() {
    if [[ $header[method] != "HEAD" ]]; then
        local buff

        # Buffer output into 8K chunks using a pipe and sysread
        while sysread buff; do
            printf '%x\r\n' "$(print ${#buff})"
            printf '%s\r\n' "$buff"
        done

        printf '%x\r\n' "0"
        printf '\r\n'
    fi
}

function check_if_cgi() {
    [[ -z $CGI_EXTS_ARRAY ]] && return 1

    for i in $CGI_EXTS_ARRAY; [[ ${1##*.} == $i ]] && break

    [[ $? != 0 || ! -x $1 ]] && return 1

    return 0
}

function exec_cgi() {
    unset cgi_head cgi_status_code
    typeset -ga cgi_head
    typeset -g cgi_status_code

    local cmd pid

    local -x CONTENT_LENGTH="${header[content-length]:-NULL}" CONTENT_TYPE="$header[content-type]" GATEWAY_INTERFACE="$GATEWAY_INTERFACE" QUERY_STRING="${header[querystr]#\?}" REMOTE_ADDR="$client_ip" REMOTE_HOST="NULL" REQUEST_METHOD="$header[method]" SCRIPT_NAME="${1#$DOCROOT}" SERVER_NAME="$SERVER_NAME" SERVER_ADDR="$SERVER_ADDR" SERVER_PORT="$PORT" SERVER_PROTOCOL="$SERVER_PROTOCOL" SERVER_SOFTWARE="$SERVER_SOFTWARE"

    local -x DOCUMENT_ROOT="$DOCROOT" REQUEST_URI="$header[url]$header[querystr]" SCRIPT_FILENAME="$1" REDIRECT_STATUS=1

    for i in ${(k)header}; do
        case $i in
            ("connection"|"content-length"|"content-type"|"querystr")
                continue;;
            ("method"|"version"|"url")
                continue;;
            (*)
                local -x HTTP_${(U)i:gs/\-/\_}="$header[$i]";;
        esac
    done

    [[ ${1##*.} == "php" ]] && cmd="php-cgi"

    logger_child "Executing cgi script $1"
    coproc { timeout $cmd "$1" <&$fd }
    pid=$!

    while read -r -p line; do
        [[ -z $line || $line == $'\r' ]] && break
        [[ $line =~ "Status:*" ]] && cgi_status_code=${line#Status: }
        cgi_head+=${line%$'\r'}
    done

    if [[ -z ${(M)${cgi_head:l}:#content-type*} ]]; then
        logger_child "ERROR CGI script $1 failed to return MIME-type"
        return 1
    fi

    return_header ${cgi_status_code:-"200 Ok"} "Transfer-Encoding: chunked" $cgi_head[@]
    send_chunk <&p

    wait $pid || { logger_child "ERROR executing CGI script $1"; return 1 }
}

function handle_file() {
    if check_if_cgi $1; then
        exec_cgi $1
    else
        local -A file_array
        stat -H file_array $1

        local mtype
        mtype=$(mime_type $1)

        if check_if_compression $1; then
            send_compressed_file $1
        else
            send_raw_file $1
        fi
    fi
}

function srv() {
    pathname="${DOCROOT}$(urldecode $header[url])"
    { if [[ -f $pathname ]]; then
        [[ $options[globdots] == off && ${pathname:t} == .* ]] && { error_header 403; return }

        handle_file $pathname
    elif [[ -d $pathname ]]; then
        [[ $FOLLOW_SYMLINKS == 0 && -h ${pathname%/} ]] && { error_header 403; return }

        if [[ -f $pathname/$INDEX_FILE ]]; then
            handle_file $pathname/$INDEX_FILE
        else
            if [[ $pathname != $DOCROOT && $pathname[-1] != "/" ]]; then
                error_header 301 "http://$SERVER_ADDR:$PORT${pathname#$DOCROOT}/"
                return
            else
                send_list "$pathname" || { error_header 403; return }
            fi
        fi
    else
        error_header 404
        return
    fi } || { error_header 500; return }

    logger_child "200"
}

function cleanup() {
    for i in ${(SM)jobstates##<->}; do
        kill $i 2>/dev/null
    done

    logger "> Exiting main"
    exec {logfd}>&-; exec {fifofd}>&-
    rm $FIFO; ztcp -c
}

function TRAPCHLD() {
    # Flush fifo data so we don't fill up the pipe (do we really care about the child's exit status?)
    while sysread -t 0 -i $fifofd -o 1 1>/dev/null; do
        continue
    done
}

# Main
trap "cleanup; exit" INT KILL TERM

# By default discard logging output
exec {logfd}>/dev/null

# Parse cli options and config file
parse_args $*

# Set defaults
: ${MAX_CONNECTIONS:=12}
: ${PORT:=8080}
: ${HTTP_KEEP_ALIVE:=1}
: ${HTTP_TIMEOUT:=30}
: ${FOLLOW_SYMLINKS:=0}
: ${INDEX_FILE:=index.html}
: ${CGI_EXTS_ARRAY:=""}
: ${CGI_TIMEOUT:=300}
: ${FIFO:=/tmp/czhttpd.fifo}

if [[ $COMPRESSION == 1 ]]; then
    GZIP_TYPES_ARRAY+=("text/html" "text/css" "text/javascript")
fi

# Immutable Vars
readonly SERVER_NAME="$(ip_addr)"
readonly SERVER_ADDR="$SERVER_NAME"
readonly SERVER_SOFTWARE="czhttpd/1.2b"
readonly SERVER_PROTOCOL="HTTP/1.1"
readonly GATEWAY_INTERFACE="CGI/1.1"

logger "MAX_CONNECTIONS: $MAX_CONNECTIONS"
logger "PORT: $PORT"
logger "KEEP_ALIVE: $HTTP_KEEP_ALIVE"
logger "TIMEOUT: $HTTP_TIMEOUT"
logger "FOLLOW_SYMLINKS: $FOLLOW_SYMLINKS"
logger "INDEX_FILE: $INDEX_FILE"
logger "COMPRESSION: $COMPRESSION"
logger "COMPRESSED_TYPES: $GZIP_TYPES_ARRAY"
logger "CGI_EXTENSIONS: ${CGI_EXTS_ARRAY:-"N/A"}"
logger "CGI_TIMEOUT: $CGI_TIMEOUT"
logger "HIDDEN_FILES: $options[globdots]"

http_listen

mkfifo $FIFO 2>/dev/null || error "Can not create $FIFO"
exec {fifofd}<>$FIFO

while :; do
    # Use select to block until a child writes to fifo on exit
    while (( ${#jobstates} >= $MAX_CONNECTIONS )); do
        logger "max connections hit, temporarily unable to fork"
        zselect -r $fifofd && break
    done

    ztcp -a $listenfd

    ( trap "ztcp -c; exit" KILL TERM; trap '' PIPE

    fd=$REPLY

    while read line; do
        [[ $line[3] == "I" ]] && { client_ip=${line[(ws. .)3]}; break }
    done < <(ztcp -L)

    while :; do
        parse_request || { logger_child "empty request, closing"; break }

        if check_request; then
            srv 1>&$fd

            if [[ $HTTP_KEEP_ALIVE == 0 || $header[Connection] == "close" ]]; then
                logger_child "connection type: close, closing"
                break
            fi
        fi
    done

    ztcp -c

    print "0" >&$fifofd ) &

    ztcp -c $REPLY
done
